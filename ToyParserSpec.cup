import java_cup.runtime.*;

parser code {:
    ToyScanner scanner;
	
    Parser(ToyScanner scanner) { this.scanner = scanner; }
	
	public void syntax_error(Symbol cur_token) {
		done_parsing();
	}
	
	public void unrecovered_syntax_error(Symbol cur_token) {
		System.out.println("[reject]");
	}
:};

scan with {: 
	return scanner.next_token(); 
:};

/* Terminals (tokens returned by the scanner). */
terminal	_boolean, _double, _int, _string;
terminal	_if, _else, _for, _while;
terminal	_intconstant, _doubleconstant, _stringconstant, _booleanconstant;
terminal	_plus, _minus, _multiplication, _division, _mod, _uminus;
terminal	_leftparen, _rightparen, _leftbracket, _rightbracket, _leftbrace, _rightbrace;
terminal	_less, _lessequal, _greater, _greaterequal, _equal, _notequal;
terminal	_and, _or, _not;
terminal	_assignop, _semicolon, _comma, _period;
terminal	_break, _class, _extends, _implements, _interface, _newarray;
terminal	_println, _readln, _return, _void;
terminal	_id;

/* Non terminals */
non terminal	Program, Decls, Decl;
non terminal	VariableDecl, FunctionDecl, ClassDecl, InterfaceDecl;
non terminal	Variable, Type, Formals, Variables, Extends, Implements, Implement;
non terminal	Field, Fields, Prototype, StmtBlock, VariableDecls, Stmts, Stmt;
non terminal	OptionExpr, WhileStmt, ForStmt, BreakStmt;
non terminal	ReturnStmt, PrintStmt, Expr, Exprs, Lvalue, Call, Actuals, Constant;
non terminal	IfStmt;

/* Precedences */
precedence right _assignop;
precedence left _or;
precedence left _and;
precedence left _equal, _notequal;
precedence left _less, _lessequal, _greater, _greaterequal;
precedence left _plus, _minus;
precedence left _multiplication, _division, _mod;
precedence left _not;
precedence left _leftbracket, _period;
precedence left _else;

/* Toy grammar */

start with Program;

Program ::= 	
	Decls 
		{: 	System.out.print("[reduce 1]"); System.out.print("[accept]"); done_parsing(); :};

Decls ::= 
	Decl
		{: System.out.print("[reduce 2]"); :} 
	| Decl Decls
		{: System.out.print("[reduce 3]"); :} ;

Decl ::=
	VariableDecl 	
		{: System.out.print("[reduce 4]"); :} 
	| FunctionDecl 	
		{: System.out.print("[reduce 5]"); :} 
	| ClassDecl 		
		{: System.out.print("[reduce 6]"); :} 
	| InterfaceDecl	
		{: System.out.print("[reduce 7]"); :} ;

VariableDecl ::=
	Variable _semicolon	
		{: System.out.print("[reduce 8]"); :} ;

Variable ::=
	Type _id
		{: System.out.print("[reduce 9]"); :} ;

Type ::=
	_int 
		{: System.out.print("[reduce 10]"); :} 
	| _double
		{: System.out.print("[reduce 11]"); :} 
	| _boolean
		{: System.out.print("[reduce 12]"); :} 
	| _string
		{: System.out.print("[reduce 13]"); :} 
	| Type _leftbracket _rightbracket 	
		{: System.out.print("[reduce 14]"); :} 
	| _id												
		{: System.out.print("[reduce 15]"); :} ;

FunctionDecl ::= 
	Type _id _leftparen Formals _rightparen StmtBlock 
		{: System.out.print("[reduce 16]"); :} 
	| _void _id _leftparen Formals _rightparen StmtBlock
		{: System.out.print("[reduce 17]"); :} ;
						
Formals ::=
	// EMPTY
		{: System.out.print("[reduce 18]"); :} 
	| Variables
		{: System.out.print("[reduce 19]"); :} ;

Variables ::= 
	Variable
		{: System.out.print("[reduce 20]"); :}  
	| Variable _comma Variables	
		{: System.out.print("[reduce 21]"); :} ;

ClassDecl ::= 
	_class _id Extends Implements _leftbrace Fields _rightbrace
		{: System.out.print("[reduce 22]"); :} ;

Extends ::=
	// EMPTY
		{: System.out.print("[reduce 23]"); :}
	| _extends _id
		{: System.out.print("[reduce 24]"); :};

Implements ::= 
	// EMPTY
		{: System.out.print("[reduce 25]"); :}
	| Implement
		{: System.out.print("[reduce 26]"); :};

Implement ::= 
	_implements _id 
		{: System.out.print("[reduce 27]"); :}
	| _implements _id _comma Implement
		{: System.out.print("[reduce 28]"); :};

Fields ::= 	
	// EMPTY
		{: System.out.print("[reduce 29]"); :}
	| Field Fields
		{: System.out.print("[reduce 30]"); :};

Field ::= 
	VariableDecl
		{: System.out.print("[reduce 31]"); :} 
	| FunctionDecl	
		{: System.out.print("[reduce 32]"); :};

InterfaceDecl ::= 
	_interface _id _leftbrace Prototype _rightbrace	
		{: System.out.print("[reduce 33]"); :};

Prototype ::=
	// EMPTY
		{: System.out.print("[reduce 34]"); :}
	| Type _id _leftparen Formals _rightparen _semicolon Prototype 
		{: System.out.print("[reduce 35]"); :}
	| _void _id _leftparen Formals _rightparen _semicolon Prototype
		{: System.out.print("[reduce 36]"); :};
					
StmtBlock ::= 
	_leftbrace VariableDecls Stmts _rightbrace	
		{: System.out.print("[reduce 37]"); :};

VariableDecls ::=
	// EMPTY
		{: System.out.print("[reduce 38]"); :}
	| VariableDecl VariableDecls
		{: System.out.print("[reduce 39]"); :};

Stmts ::=
	// EMPTY
		{: System.out.print("[reduce 40]"); :}
	| Stmt Stmts
		{: System.out.print("[reduce 41]"); :};

Stmt ::= 
	OptionExpr _semicolon 
		{: System.out.print("[reduce 42]"); :}
	| IfStmt 
		{: System.out.print("[reduce 43]"); :}
	| WhileStmt 
		{: System.out.print("[reduce 44]"); :}
	| ForStmt 	
		{: System.out.print("[reduce 45]"); :}
	| BreakStmt
		{: System.out.print("[reduce 46]"); :}
	| ReturnStmt 
		{: System.out.print("[reduce 47]"); :}
	| PrintStmt 
		{: System.out.print("[reduce 48]"); :}
	| StmtBlock
		{: System.out.print("[reduce 49]"); :};

IfStmt ::= 
	_if _leftparen Expr _rightparen Stmt	
		{: System.out.print("[reduce 50]"); :} 
	|  _if _leftparen Expr _rightparen Stmt _else Stmt	
		{: System.out.print("[reduce 51]"); :}; 

WhileStmt ::=
	_while _leftparen Expr _rightparen Stmt
		{: System.out.print("[reduce 52]"); :};

ForStmt ::=
	_for _leftparen OptionExpr _semicolon Expr _semicolon OptionExpr _rightparen Stmt	
		{: System.out.print("[reduce 53]"); :};

BreakStmt ::= 
	_break _semicolon
		{: System.out.print("[reduce 54]"); :};

ReturnStmt ::= 
	_return OptionExpr _semicolon		
		{: System.out.print("[reduce 55]"); :};

PrintStmt ::= 
	_println _leftparen Exprs _rightparen _semicolon	
		{: System.out.print("[reduce 56]"); :};
		
Expr ::= 
	Lvalue _assignop Expr 
		{: System.out.print("[reduce 57]"); :}
	| Constant 
		{: System.out.print("[reduce 58]"); :}
	| Lvalue
		{: System.out.print("[reduce 59]"); :}
	| Call 
		{: System.out.print("[reduce 60]"); :}
	| _leftparen Expr _rightparen 
		{: System.out.print("[reduce 61]"); :}
	| Expr _plus Expr 	
		{: System.out.print("[reduce 62]"); :}
	| Expr _minus Expr 
		{: System.out.print("[reduce 63]"); :}
	| Expr _multiplication Expr 
		{: System.out.print("[reduce 64]"); :}
	| Expr _division Expr 
		{: System.out.print("[reduce 65]"); :}
	| Expr _mod Expr 	
		{: System.out.print("[reduce 66]"); :}
	| _uminus Expr 
		{: System.out.print("[reduce 67]"); :}
	| Expr _less Expr 	
		{: System.out.print("[reduce 68]"); :}
	| Expr _lessequal Expr
		{: System.out.print("[reduce 69]"); :}
	| Expr _greater Expr 
		{: System.out.print("[reduce 70]"); :}
	| Expr _greaterequal Expr 
		{: System.out.print("[reduce 71]"); :}
	| Expr _equal Expr 
		{: System.out.print("[reduce 72]"); :}
	| Expr _notequal Expr 
		{: System.out.print("[reduce 73]"); :}
	| Expr _and Expr 
		{: System.out.print("[reduce 74]"); :}
	| Expr _or Expr 
		{: System.out.print("[reduce 75]"); :}
	| _not Expr 
		{: System.out.print("[reduce 76]"); :}
	| _readln _leftparen _rightparen 
		{: System.out.print("[reduce 77]"); :}
	| _newarray _leftparen _intconstant _comma Type _rightparen
		{: System.out.print("[reduce 78]"); :};
			
Lvalue ::= 
	_id
		{: System.out.print("[reduce 79]"); :}
	| Lvalue _leftbracket Expr _rightbracket 
		{: System.out.print("[reduce 80]"); :}
	| Lvalue _period _id
		{: System.out.print("[reduce 81]"); :};

Call ::= 
	_id _leftparen Actuals _rightparen 
		{: System.out.print("[reduce 82]"); :}
	| _id _period _id _leftparen Actuals _rightparen
		{: System.out.print("[reduce 83]"); :};

Actuals ::=
	// EMPTY
		{: System.out.print("[reduce 84]"); :} 
	| Exprs	
		{: System.out.print("[reduce 85]"); :};

Exprs ::= 
	Expr
		{: System.out.print("[reduce 86]"); :}
	| Expr _comma Exprs
		{: System.out.print("[reduce 87]"); :};

Constant ::= 
	_intconstant
		{: System.out.print("[reduce 88]"); :}
	| _doubleconstant
		{: System.out.print("[reduce 89]"); :}
	| _stringconstant 
		{: System.out.print("[reduce 90]"); :}
	| _booleanconstant
		{: System.out.print("[reduce 91]"); :};
					
OptionExpr ::= 
	//EMPTY
		{: System.out.print("[reduce 92]"); :}
	| Expr
		{: System.out.print("[reduce 93]"); :};